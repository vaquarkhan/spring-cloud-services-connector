buildscript {
	repositories {
		maven { url "https://repo.spring.io/plugins-release" }
		mavenCentral()
	}

	dependencies {
		classpath("org.springframework.build.gradle:propdeps-plugin:0.0.7")
		classpath("io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE")
		classpath("io.spring.gradle:spring-io-plugin:0.0.4.RELEASE")
	}
}

allprojects {
	group = "io.pivotal.spring.cloud"

	ext.springBootVersion = "1.4.1.RELEASE"
	ext.springCloudVersion = "Camden.BUILD-SNAPSHOT"
	ext.lombokVersion = "1.16.8"

	apply plugin: "propdeps"
	apply plugin: "propdeps-eclipse"
	apply plugin: "propdeps-idea"
	apply plugin: "java"
	apply plugin: "eclipse"
	apply plugin: "idea"
	apply plugin: "io.spring.dependency-management"

	dependencyManagement {
		imports {
			mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	if (project.hasProperty('platformVersion')) {
		apply plugin: 'spring-io'

		// necessary to resolve the Spring IO versions (which may include snapshots)
		repositories {
			maven { url "https://repo.spring.io/libs-snapshot" }
		}

		dependencyManagement {
			springIoTestRuntime {
				imports {
					mavenBom "io.spring.platform:platform-bom:${platformVersion}"
				}
			}
		}
	}

	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/libs-release" }
		maven { url "https://repo.spring.io/libs-snapshot" }
	}

	dependencies {
		testCompile("org.springframework.boot:spring-boot-starter-test")
	}

	compileJava {
		sourceCompatibility=1.7
		targetCompatibility=1.7
	}
	compileTestJava {
		sourceCompatibility=1.8
		targetCompatibility=1.8
	}

	ext.javadocLinks = [
			'http://docs.oracle.com/javase/8/docs/api/',
			'http://docs.oracle.com/javaee/7/api/',
			'http://docs.spring.io/spring/docs/current/javadoc-api/',
	] as String[]
}

subprojects {
	apply from: "${rootProject.projectDir}/publish-maven.gradle"

	task allDependencies(type: DependencyReportTask)

	jar {
		manifest.attributes["Created-By"] =
				"${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
		manifest.attributes["Implementation-Title"] = project.name
		manifest.attributes["Implementation-Version"] = project.version

		from("${rootProject.projectDir}/src/dist") {
			include "license.txt"
			include "notice.txt"
			into "META-INF"
			expand(copyright: new Date().format("yyyy"), version: project.version)
		}
	}

	javadoc {
		description = "Generates project-level javadoc for use in -javadoc jar"

		options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
		options.author = true
		options.header = project.name
		options.links(project.ext.javadocLinks)
		if (JavaVersion.current().isJava8Compatible()) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	task javadocJar(type: Jar) {
		classifier = "javadoc"
		from javadoc
	}

	artifacts {
		archives sourcesJar
		archives javadocJar
	}
}

project(":spring-cloud-services-connector-core") {
	description = "Spring Cloud Services Connector Core"
	dependencies {
		compile("org.springframework:spring-core")
		compile("org.springframework.cloud:spring-cloud-core")
	}
}

project(":spring-cloud-services-cloudfoundry-connector") {
	description = "Spring Cloud Services Cloud Foundry Connector"
	dependencies {
		compile(project(":spring-cloud-services-connector-core"))
		compile("org.springframework.cloud:spring-cloud-cloudfoundry-connector")
		testCompile(group: "org.springframework.cloud", name: "spring-cloud-cloudfoundry-connector", classifier: "tests")
		testCompile("com.fasterxml.jackson.core:jackson-databind")
	}
}

project(":spring-cloud-services-spring-connector") {
	description = "Spring Cloud Services Spring Connector"
	dependencies {
		compile(project(":spring-cloud-services-connector-core"))
		compile("org.springframework.cloud:spring-cloud-spring-service-connector")
		compile("org.projectlombok:lombok:${lombokVersion}")
		optional("org.springframework.cloud:spring-cloud-config-client")
		optional("org.springframework.cloud:spring-cloud-netflix-eureka-client")
		optional("com.netflix.eureka:eureka-client")
		optional("org.springframework.amqp:spring-rabbit")
		optional("com.sun.jersey:jersey-client")
		optional("org.springframework.security.oauth:spring-security-oauth2")
		testCompile("org.apache.tomcat.embed:tomcat-embed-el")
		testCompile(group: "org.springframework.cloud", name: "spring-cloud-commons")
		testCompile(group: "org.springframework.cloud", name: "spring-cloud-core", classifier: "tests")
		testCompile("org.springframework.cloud:spring-cloud-cloudfoundry-connector")
		testCompile(group: "org.springframework.cloud", name: "spring-cloud-cloudfoundry-connector", classifier: "tests")
		testCompile("org.springframework.cloud:spring-cloud-starter-hystrix")
		testCompile("org.springframework.cloud:spring-cloud-netflix-hystrix-stream")
		testCompile("org.springframework.cloud:spring-cloud-starter-stream-rabbit")
		testCompile("commons-logging:commons-logging")
	}
}

configure(rootProject) {
	description = "Spring Cloud Services Connector"

	// do not publish the default jar for the root project
	configurations.archives.artifacts.clear()

	dependencies { // for integration tests
	}

	task api(type: Javadoc) {
		group = "Documentation"
		description = "Generates aggregated Javadoc API documentation."
		title = "${rootProject.description} ${version} API"
		options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
		options.author = true
		options.header = rootProject.description
		options.overview = "src/api/overview.html"
		options.links(project.ext.javadocLinks)
		if (JavaVersion.current().isJava8Compatible()) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}

		source subprojects.collect { project ->
			project.sourceSets.main.allJava
		}

		classpath = files(subprojects.collect { project ->
			project.sourceSets.main.compileClasspath
		})

		maxMemory = "1024m"
		destinationDir = new File(buildDir, "api")
	}

	task docsZip(type: Zip) {
		group = 'Distribution'
		classifier = 'docs'
		description = "Builds -${classifier} archive containing api and reference " +
				"for deployment at docs.spring.io."

		from(api) {
			into 'api'
		}
	}

	task distZip(type: Zip, dependsOn: docsZip) {
		group = "Distribution"
		classifier = "dist"
		description = "Builds -${classifier} archive, containing all jars and docs, " +
				"suitable for community download page."

		def baseDir = "${project.name}-${project.version}";

		from("src/dist") {
			include "license.txt"
			include "notice.txt"
			into "${baseDir}"
			expand(copyright: new Date().format("yyyy"), version: project.version)
		}

		from(zipTree(docsZip.archivePath)) {
			into "${baseDir}/docs"
		}

		subprojects.each { subproject ->
			into ("${baseDir}/libs") {
				from subproject.jar
				if (subproject.tasks.findByPath("sourcesJar")) {
					from subproject.sourcesJar
				}
				if (subproject.tasks.findByPath("javadocJar")) {
					from subproject.javadocJar
				}
			}
		}
	}

	artifacts {
		archives docsZip
		archives distZip
	}

	task wrapper(type: Wrapper) {
		gradleVersion = '2.14.1'
	}
}
